apply plugin: 'com.android.application'

android {
  // app can use the API features included in this API level and lower.
  compileSdkVersion 27

  // can override some attributes in main/AndroidManifest.xml
  defaultConfig {
    applicationId 'net.sourceforge.smallbasic'
    minSdkVersion 16
    targetSdkVersion 27
    versionCode 36
    versionName "0.12.15.3"
    resConfigs "en"
  }

  signingConfigs {
    release {
      Properties properties = new Properties()
      properties.load(project.rootProject.file('local.properties').newDataInputStream())
      storeFile file(properties.getProperty('storeFile'))
      keyAlias properties.getProperty('keyAlias')
      storePassword properties.getProperty('storePassword')
      keyPassword properties.getProperty('keyPassword')
    }
  }

  buildTypes {
    debug {
      buildConfigField "boolean", "DEBUG_VIEW", "true"
      buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }
    release {
      buildConfigField "boolean", "DEBUG_VIEW", "false"
      buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  externalNativeBuild {
    ndkBuild {
      path '../jni/Android.mk'
    }
  }
}

dependencies {
  implementation 'com.android.support:support-compat:27.1.1'
  testImplementation 'junit:junit:4.12'
}
